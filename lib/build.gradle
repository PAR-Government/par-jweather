plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

repositories {
    mavenCentral()
}

version = '1.0.0'
group = 'io.github.par-government'

shadowJar {
    classifier = ''
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifacts = [shadowJar, sourceJar, javadocJar]

            pom {
                name = 'PAR JWeather'
                description = 'Derivation of the original JWeather library from https://github.com/arimus/jweather'
                url = 'https://github.com/PAR-Government/par-jweather'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                    }
                }
                developers {
                    developer {
                        name = 'Gregory Hasseler'
                        email = 'gregory_hasseler@partech.com'
                        organization = 'PAR Government Systems Corporation'
                        organizationUrl = 'https://pargovernment.net/'
                    }
                    developer {
                        name = 'Scott Auman'
                        email = 'scott_auman@partech.com'
                        organization = 'PAR Government Systems Corporation'
                        organizationUrl = 'https://pargovernment.net/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/PAR-Government/par-jweather.git'
                    developerConnection = 'scm:git:ssh://github.com:PAR-Government/par-jweather.git'
                    url = 'https://github.com/PAR-Government/par-jweather'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'sonatype'
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "$mavenUser"
                password "$mavenPassword"
            }
        }
    }
}

signing {
    sign publishing.publications.shadow
}

dependencies {
    implementation fileTree(dir: 'libs', includes: ['*.jar'])

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}
